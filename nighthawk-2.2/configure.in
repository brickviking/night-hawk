dnl Process this file with autoconf to produce a configure script. -*- m4 -*-

###
### Initialization
###
AC_INIT(data/Mearkat/Hull.m)
AM_INIT_AUTOMAKE(nighthawk,2.3)
AM_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr/local)
AC_CANONICAL_HOST

###
### Options
### 
ACWS_WITHOUT_SCORES
ACWS_DISABLE_SLOWDEFS
ACWS_DISABLE_RED_SPRITES
ACWS_DISABLE_SOUND
ACWS_DISABLE_MONO
ACWS_DISABLE_8BIT
ACWS_WITHOUT_SAM_RATE
ACWS_WITHOUT_FX_CHANNELS
ACWS_DISABLE_TOOLS

###
### Checks for programs.
###
AC_PROG_CC
ACWS_CHECK_CXX(enabled)

AM_PROG_INSTALL
AC_PROG_MAKE_SET

###
### Libraries
### 
AC_CHECK_LIB(m, sin)
ACWS_CHECK_LIBS_X
ACWS_CHECK_LIBS_XEXT

###
### Headers
### 
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h malloc.h sys/file.h sys/ioctl.h sys/time.h unistd.h)

###
### Checks for typedefs, structures, and compiler characteristics.
### 
AC_HEADER_TIME

###
### Checks for library functions.
### 
AC_PROG_GCC_TRADITIONAL
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(select)

###
### Setup
### 
AC_SUBST(DEFS)
AC_SUBST(ADD_DEFS)

AC_SUBST(INCLUDES)
INCLUDES='-I$(top_builddir) -I$(top_srcdir)'

AC_SUBST(INSTALL_DIR)
INSTALL_DIR="\$(pkgdatadir)"
ADD_DEFS="$ADD_DEFS"' -DINSTALL_DIR="\"$(INSTALL_DIR)\""'

###
### Options
###

AC_SUBST(SCORES_FILE)
if test x"$with_scores" = xno; then
   SCORES_FILE=".nighthawk.scores"
else
   SCORES_FILE="$with_scores"
fi
ADD_DEFS="$ADD_DEFS"' -DSCORES_FILE="\"$(SCORES_FILE)\""'

if test x"$enable_red_sprites" = xyes; then
   REDUCED_SPRITES=1
   AC_DEFINE(REDUCED_SPRITES)
fi

AC_SUBST(NIGHTHAWK_SOUND)
if test x"$enable_sound" = xyes; then
   NIGHTHAWK_SOUND=nighthawk_sound
else
   NIGHTHAWK_SOUND=
fi

if test x"$enable_mono" = xyes; then
   status_mono_stereo=mono
   DEFAULT_STEREO=0
else
   status_mono_stereo=stereo
   DEFAULT_STEREO=1
fi
AC_DEFINE_UNQUOTED(DEFAULT_STEREO, $DEFAULT_STEREO)

if test x"$enable_8bit" = xyes; then
   DEFAULT_PRECISION="8"
else
   DEFAULT_PRECISION="16"
fi
AC_DEFINE_UNQUOTED(DEFAULT_PRECISION, $DEFAULT_PRECISION)

if test x"$with_sam_rate" = xno; then
   with_sam_rate=22050
fi
DEFAULT_SAM_RATE="$with_sam_rate"
AC_DEFINE_UNQUOTED(DEFAULT_SAM_RATE, $DEFAULT_SAM_RATE)

if test x"$with_fx_channels" = xno; then
   with_fx_channels=8
fi
NO_FX_CHANNELS="$with_fx_channels"
AC_DEFINE_UNQUOTED(NO_FX_CHANNELS, $NO_FX_CHANNELS)

AC_SUBST(NIGHTHAWK_TOOLS)
NIGHTHAWK_TOOLS=
#don't enable this unless you want to include the map editor and demo recorder
#NIGHTHAWK_TOOLS="ned xpm_view"

###
### Output
###
AC_OUTPUT(
src_sound/Makefile src/Makefile doc/Makefile Makefile
data/Makefile
data/Anoyle/Makefile
data/Discovery/Makefile
data/Esperence/Makefile
data/Friendship/Makefile
data/Haldeck/Makefile
data/Mearkat/Makefile
data/Ophukus/Makefile
data/Seafarer/Makefile
data/Tobruk/Makefile
data/Zaxon/Makefile
data/fx/Makefile
data/music/Makefile
data/xpm/Makefile
data/xpm/standard/Makefile
)

###
### Status
###
AC_MSG_RESULT([### Compile Options ###])
AC_MSG_RESULT([# prefix:          $prefix])
AC_MSG_RESULT([# scores file:     $SCORES_FILE])
AC_MSG_RESULT([# reduced sprites: $enable_red_sprites])
AC_MSG_RESULT([# sound:           $enable_sound])
if test x"$enable_sound" = xyes; then
    AC_MSG_RESULT([# mono/stereo:     $status_mono_stereo])
    AC_MSG_RESULT([# precision:       $DEFAULT_PRECISION bit])
    AC_MSG_RESULT([# sampling rate:   $DEFAULT_SAM_RATE Hz])
    AC_MSG_RESULT([# FX channels:     $NO_FX_CHANNELS])
fi
AC_MSG_RESULT([# make tools:      $enable_tools])
